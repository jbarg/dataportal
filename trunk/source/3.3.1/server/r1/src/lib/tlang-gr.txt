/**************************************************************************
Copyright ^Ó General Atomics, 1995-1999
All Rights Reserved
 
This file contains Original Code and/or Modifications of Original Code as
defined in and that are subject to the General Atomics Storage Resource
Broker Academic and Research Use  License (the 'License'). This license is
for academic and research use only. You may not use, copy, modify or
distribute SRB for commercial purposes, nor incorporate the SRB into
commercial products without the express written permission of General
Atomics nor may you distribute SRB to any third party.
 
You may not use this software except in compliance with the License. Please
obtain a copy of the License at http://www.npaci.edu/DICE/SRB/License and
read it before using this file.
 
The Original Code and all software distributed under the License are
distributed on an 'AS IS' basis, WITHOUT WARRANTY OF ANY KIND, EITHER
EXPRESS OR IMPLIED, AND GENERAL ATOMICS AND THE SAN DIEGO SUPERCOMPUTER
CENTER HEREBY DISCLAIM ALL SUCH WARRANTIES, INCLUDING WITHOUT LIMITATION,
ANY WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE OR
NON-INFRINGEMENT OF ANY COPYRIGHT, PATENT , TRADEMARK OR OTHER RIGHTS.
GENERAL ATOMICS AND THE SAN DIEGO SUPERCOMPUTER CENTER FURTHER  HAVE NO
OBLIGATION TO PROVIDE MAINTENANCE, SUPPORT, UPDATES, ENHANCMENTS OR
MODIFICATIONS.. Please see the License for the specific language governing
rights and limitations under the License."
 
Please read the terms of this license carefully. By using the SRB software,
you are accepting and agreeing to the terms of this license with General
Atomics. If you are agreeing to this license on behalf of a company, or
other institution, you represent that you are authorized to bind the
company/institution to such a license. If you do not meet this criterion or
you do not agree to any of the terms of this license, you are not
authorized to use the software.
**************************************************************************/
 


/*
 * TableSidMisc.c - Misc routines for the srbServer and srbMaster TableSID.
 */

Grammar for Template Language (T-Language)
==========================================

<tlTemplate>		 := <tlMainTemplate><tlFunctionsTemplate>
<tlMainTemplate>         := [<tlTemplateHeader>][<tlTemplateBody>][<tlTemplateFooter>][<tlTemplateRules>]
<tlTemplateHeader>       := '<TLHEAD>'<tlString>'</TLHEAD>'
<tlTemplateBody>         := '<TLBODY>'<tlString>'</TLBODY>'
<tlTemplateFooter>       := '<TLTAIL>'<tlString>'</TLTAIL>'
<tlTemplateRules>	 := '<TLRULES>'<tlApplyRules><tlRuleSet>'</TLRULES>'

<tlFunctionsTemplate>    := <empty-string> |
			    '<TLFUNCLIST>'<tlFuncTemplateList>'</TLFUNCLIST>'
<tlFuncTemplateList>	 := <empty-string> |
			    <tlFuncTemplate><tlFuncTemplateList>
<tlFuncTemplate>	 := '<TLFUNC>'<tlFuncName>'('<tlFuncArgList>')<TLFUNCBODY>'<tlString>'</TLFUNC>'

<tlString>		 := <tlPrintString> |
			    <tlString><tlValueHolder><tlString> |
			    <tlString><tlEvalTemplate><tlString> |
			    <tlString><tlifTemplate><tlString> |
			    <tlString><tlforTemplate><tlString> |
			    <tlString><tlincludeObject><tlString> |
			    <tlString><tlfunctionCall><tlString> |
			    <tlString><tlAssignmentBlock><tlString>
<tlincludeObject>	 := '<TLINCLUDEOBJ>'<srbObjPropertiesString>'</TLINCLUDEOBJ>'
<tlAssignmentBlock>	 := '<TLASSIGN>'<tlAssignmentList>'</TLASSIGN>'
<tlfunctionCall>	 := '<TLFUNCCALL>'<tlFuncName>'('<tlFuncParamList>')</TLFUNCCALL>'
<tlifTemplate>		 := '<TLIF>'<tlLogicalExpr>'<TLTHEN>'<tlString>'<TLELSE>'<tlString>'</TLIF>' |
			    '<TLIF>'<tlLogicalExpr>'<TLTHEN>'<tlString>'</TLIF>' 
<tlforTemplate>		 := '<TLFOR>'<tlforControl>'<TLFORBODY>'<tlString>'</TLFOR>'
<tlforControl>		 := <tlAssignmentList>';'<tlLogicalExpr>';'<tlAssignmentList>
<tlAssignmentList>	 := <empty-string> |
			     <tlAssignment> |
			     <tlAssignment> ',' <tlAssignment>
<tlEvalTemplate>	 := '<TLEVAL>'<tlEvaluation>'</TLEVAL>'
<tlAssignment>		 := <tlUserDefinedNameStr> '=' <tlEvaluation>

<tlApplyRules>		 := [<tlApplyQuery>][<tlApplyCond>]
<tlApplyQuery>		 := '<TLQUERY>'<tlPrintString>'</TLQUERY>'
<tlApplyCond>		 := '<TLRULECOND>'<tlAssignmentList>'</TLRULECOND>'
<tlRuleSet>		 := <empty-string> |
			    <tlRule><tlRuleSet> 
<tlRule>		 := '<TLRULEHEAD>'<tlRegExp>'</TLRULEHEAD>'<tlValueHolder>'<TLRULETAIL>'<tlRegExp>'</TLRULETAIL>'
<tlRule>		 := '<TLRULEHEAD>'<tlRegExp>'<TLRULEPRESTRINGCOND>'<tlRegExp>'</TLRULEHEAD>'<tlValueHolder>'<TLRULETAIL>'<tlRegExp>'</TLRULETAIL>'


<srbObjPropertiesString> := <srbObjName> |
			    <srbObjConjunctString> 
<srbObjConjunctString>   := '&'<metaCatEqEqExpression> |
			    '&'<metaCatEqExpression> <srbObjConjunctString>
<metaCatEqExpression>	 := <metaCatDefineConstant>'='<metaDataValue>
<metaDataValue>		 := <number> | <quoted-String>

<tlFuncArgList>		 := <tlFuncArg> |
			    <tlFuncArg> , <tlFuncArgList>
<tlFuncParamList>	 := <tlFuncParam> |
			    <tlFuncParam> , <tlFuncParamList>
<tlFuncName>		 := <tlUserDefinedValueHolder>
<tlFuncArg>		 := <tlUserDefinedValueHolder>
<tlFuncParam>		 := <tlString>

<tlEvaluation>		 := <tlArithStringExpr>

<tlLogicalExpr>		 := <tlLogterm> |
			    <tlArithStringExpr>   --- result is coerced to 0 or 1
<tlArithStringExpr>	 := <tlASExp>  |
			    '(' <tlASExp> ')' |
			    '[' <tlASExp> ']' |
			    '{' <tlASExp> '}' 
			    
<tlRedExpr>		 := Regular Expression defined for regexp
<tlASExp>		 := <tlASterm> | 
			    <tlLogicalExpr> <LogicalOpr> <tlLogicalExpr>
			    <tlArithStringExpr> <ComparisonOpr> <tlArithStringExpr> |
			    <tlArithStringExpr> <RegExpOpr> <tlArithStringExpr> |
			    <tlArithStringExpr> <ArithmeticOpr> <tlArithStringExpr> |
			    <tlArithStringExpr> <StringOpr> <tlArithStringExpr> 
			    
<LogicalOpr>		 := '&&' | '||'
<ComparisonOpr>		 := '>' | '<' | '==' | '>=' | '<=' | '!=' 
<ArithmeticOpr>		 := '+' | '-' | '*' | '/'
<RegExpOpr>		 := '?'
<StringOpr>		 := '.' |     --- concatenation
			    '|h' |    --- (s |h n)   head of  's' upto size 'n'
			    '|t' |    --- (s |t n)   tail of  's' upto size 'n'

<tlLogterm>		 := '0' |
			    '1' | 
			    <tlValueHolder>     ---  coerced into 1 or 0

<tlASterm>		 := <tlValueHolder> | 
			    <number> |
			    <quoted-string-with-\-escape> |
			    <double-quoted-string-with-\-escape>

<tlValueHolder>          := <tlColumnNameHolder> |
			    <tldataHolder> |
			    <tlescapedataHolder> |
			    <tlolddataHolder> |
			    <tlrownumberHolder> |
			    <tlnumofColumnHolder> |
			    <tlUserDefValueHolder> |
			    <tlnumofTablesHolder> |
			    <tlnumofUsrDfValHolder> |
			    <tlQueryHolder> |
			    <tlTableNameListHolder> 

<tlColumnNameHolder>     := '@@@'<tlinteger>
<tldataHolder>		 := '$$$'<tlinteger> |
			    '$$$'<tlColumnNameValue>
<tlescapedataHolder>	 := '+++'<tlinteger> |
			    '+++'<tlColumnNameValue>
<tlolddataHolder>	 := '!!!'<tlinteger>	 |
			    '!!!'<tlColumnNameValue>		
<tlUserDefValueHolder>	 := '***'<tlUserDefinedNameStr>	
<tlrownumberHolder>	 := '%%%RN'
<tlnumofColumnHolder>	 := '%%%NC'
<tlnumofTablesHolder>	 := '%%%NT'
<tlnumofUsrDfValHolder>  := '%%%ND'
<tlQueryHolder>          := '%%%QQ'
<tlTableNameListHolder>	 := '%%%TL'

<tlColumnNameValue>      := <tlPrintString>           ; stands for output columnname
<tlUserDefinedNameStr>	 := <tlPrintString>           ;   user-defined variable name

<tlPrintString>          := <empty-string> |
			    <string-without-tlReservedString>

<tlReservedString>       := '<TL' |'</TL' | '!!!' | '@@@' | '###' | '$$$' | '%%%' | '^^^' | 
			    '&&&'  | '***' | '???' | '///' | '\\\' | '+++' | '---' | 
			    '~~~'  | '|||'
<metaCatDefineConstant>	 :=  --- terms defined by Sattrs command

